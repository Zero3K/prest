; EncodePointer and DecodePointer functions (kernel32.dll) are not available
; in Win2k and WinXP without SP3, but builds generated by VS2010 depend
; on them (they are used by CRT code).
; This file overrides IAT symbols for EncodePointer and DecodePointer with
; code that uses these functions if kernel32.dll has them and simply returns
; input argument on older systems.
;
; compile with: ml.exe /c noencodepointer.asm

.MODEL FLAT, C

EXTERN __imp__LoadLibraryA@4:DWORD
EXTERN __imp__GetProcAddress@8:DWORD
EXTERN __imp__FreeLibrary@4:DWORD

PUBLIC __imp__EncodePointer@4
PUBLIC __imp__DecodePointer@4

.DATA
__imp__EncodePointer@4 DD EncpFirstrun
__imp__DecodePointer@4 DD DecpFirstrun
dll_name DB 'KERNEL32.DLL',0
encp_name DB 'EncodePointer',0
decp_name DB 'DecodePointer',0

.CODE
; EncodePointer/DecodePointer for older systems
Dummy PROC
    mov eax, [esp+4]
    retn 4
Dummy ENDP

; check if kernel32.dll has EncodePointer and DecodePointer and
; setup __imp__EncodePointer and __imp__DecodePointer accordingly
Init PROC
    push eax
    push esi
    push ebx

    mov eax, Dummy
    mov [__imp__EncodePointer@4], eax
    mov [__imp__DecodePointer@4], eax

    push OFFSET dll_name
    call [__imp__LoadLibraryA@4]
    test eax, eax
    je Init_end
    mov esi, eax

    push OFFSET encp_name
    push esi
    call [__imp__GetProcAddress@8]
    test eax, eax
    je Init_unload
    mov ebx, eax

    push OFFSET decp_name
    push esi
    call [__imp__GetProcAddress@8]
    test eax, eax
    je Init_unload

    mov [__imp__EncodePointer@4], ebx
    mov [__imp__DecodePointer@4], eax

Init_unload:
    push esi
    call [__imp__FreeLibrary@4]

Init_end:
    pop ebx
    pop esi
    pop eax
    ret
Init ENDP

; handles first call to EncodePointer
EncpFirstrun PROC
    push ebp
    mov ebp,esp
    call Init
    pop ebp
    jmp [__imp__EncodePointer@4]
EncpFirstrun ENDP

; handles first call to DecodePointer (if called before EncodePointer)
DecpFirstrun PROC
    push ebp
    mov ebp,esp
    call Init
    pop ebp
    jmp [__imp__DecodePointer@4]
DecpFirstrun ENDP

END
